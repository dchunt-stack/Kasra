// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © QuantVue

//@version=5
indicator('Average Session Range [QuantVue]', shorttitle = 'ASR [QuantVue]', overlay = true)

//----------settings----------//
var g0          = 'Sessions'
sesh1time       = input.session('1300-2200', 'Session Time', inline = '0', group = g0)
tz              = input.string('GMT-4', 'Time Zone', tooltip = 'GMT and UTC is the same thing \nMatch this setting to bottom right time', options = ['GMT-10', 'GMT-9', 'GMT-8', 'GMT-7', 'GMT-6', 'GMT-5', 'GMT-4', 'GMT-3', 'GMT+0', 'GMT+1', 'GMT+2', 'GMT+3', 'GMT+4', 'GMT+5', 'GMT+6', 'GMT+7', 'GMT+8', 'GMT+9', 'GMT+10', 'GMT+10:30', 'GMT+11', 'GMT+12', 'GMT+13', 'GMT+13:45'], group = g0)
length          = input.int(14, 'ASR Length')

var g1           = 'Lines'
showLines       = input.bool(true, 'Show Lines', group = g1)
showLabels      = input.bool(true, 'Show Labels', group = g1, inline = '01')
labPos          = input.string('Left', '', options=['Left', 'Right'], group= g1, inline = '01')
qtrBool         = input.bool(true, '', inline = '1', group = g1)
qtrCol          = input.color(color.lime, '0.25 ASR‏ ‏ ‏ ‏', inline = '1', group = g1)
qtrWidth        = input.int(1, '', inline = '1', group = g1)
qtrStyle        = input.string('Solid', '', ['Solid', 'Dashed', 'Dotted'], inline = '1', group = g1)
halfBool        = input.bool(true, '', inline = '2', group = g1)
halfCol         = input.color(color.green, '0.50 ASR‏ ‏ ‏ ‏', inline = '2', group = g1)
halfWidth       = input.int(1, '', inline = '2', group = g1)
halfStyle       = input.string('Solid', '', ['Solid', 'Dashed', 'Dotted'], inline = '2', group = g1)
qtr3Bool        = input.bool(true, '', inline = '3', group = g1)
qtr3Col         = input.color(color.orange, '0.75 ASR‏ ‏ ‏ ‏', inline = '3', group = g1)
qtr3Width       = input.int(1, '', inline = '3', group = g1)
qtr3Style       = input.string('Solid', '', ['Solid', 'Dashed', 'Dotted'], inline = '3', group = g1)
asrBool         = input.bool(true, '', inline = '4', group = g1)
asrCol          = input.color(color.fuchsia, '1 ASR ‏  ‏ ‏ ‏ ‏ ‏  ‏ ‏ ', inline = '4', group = g1)
asrWidth        = input.int(1, '', inline = '4', group = g1)
asrStyle        = input.string('Solid', '', ['Solid', 'Dashed', 'Dotted'], inline = '4', group = g1)
asrQtrBool      = input.bool(true, '', inline = '5', group = g1)
asrQtrCol       = input.color(color.red, '1.25 ASR‏ ‏  ‏  ‏', inline = '5', group = g1)
asrQtrWidth     = input.int(1, '', inline = '5', group = g1)
asrQtrStyle     = input.string('Solid', '', ['Solid', 'Dashed', 'Dotted'], inline = '5', group = g1)
asrHalfBool     = input.bool(true, '', inline = '6', group = g1)
asrHalfCol      = input.color(color.maroon, '1.5 ASR ‏ ‏  ‏  ‏ ‏', inline = '6', group = g1)
asrHalfWidth    = input.int(1, '', inline = '6', group = g1)
asrHalfStyle    = input.string('Solid', '', ['Solid', 'Dashed', 'Dotted'], inline = '6', group = g1)
asrQtr3Bool     = input.bool(true, '', inline = '7', group = g1)
asrQtr3Col      = input.color(color.navy, '1.75 ASR‏  ‏ ‏  ‏', inline = '7', group = g1)
asrQtr3Width    = input.int(1, '', inline = '7', group = g1)
asrQtr3Style    = input.string('Solid', '', ['Solid', 'Dashed', 'Dotted'], inline = '7', group = g1)
asrDubBool      = input.bool(true, '', inline = '8', group = g1)
asrDubCol       = input.color(color.black, '2 ASR ‏  ‏ ‏ ‏ ‏  ‏ ‏  ‏ ', inline = '8', group = g1)
asrDubWidth     = input.int(1, '', inline = '8', group = g1)
asrDubStyle     = input.string('Solid', '', ['Solid', 'Dashed', 'Dotted'], inline = '8', group = g1)

var g3          = 'ASR Label'
yPos            = input.string('Bottom', 'Position ', options = ['Top', 'Middle', 'Bottom'], inline = '00', group = g3)
xPos            = input.string('Right', ' ', options = ['Right','Center', 'Left'], inline = '00', group = g3)
tableBgColor    = input.color(color.white, 'Background Color', group = g3)
tableTextSize   = input.string('Normal', 'Size', options = ['Tiny','Small','Normal','Large','Huge'], inline = '0', group = g3)
tableTextColor  = input.color(color.black, 'Text Color', inline = '0', group = g3)

//----------methods----------//
method styler(string this)=>
    switch this
        'Solid'  => line.style_solid
        'Dashed' => line.style_dashed
        'Dotted' => line.style_dotted

method txtSize(string this)=>
    switch this
        'Tiny'      => size.tiny
        'Small'     => size.small
        'Normal'    => size.normal
        'Large'     => size.large
        'Huge'      => size.huge

//----------udt----------//
type lineDrawing
    color   levCol
    int     levWidth
    string  levStyle
    bool    showLev
    string  name

var colArr = array.from(lineDrawing.new(qtrCol, qtrWidth, qtrStyle.styler(), qtrBool, '.25 ASR'), lineDrawing.new(halfCol, halfWidth, halfStyle.styler(), halfBool, '.50 ASR'), lineDrawing.new(qtr3Col, qtr3Width, qtr3Style.styler(), qtr3Bool, '.75 ASR'),
     lineDrawing.new(asrCol, asrWidth, asrStyle.styler(), asrBool, '1 ASR'), lineDrawing.new(asrQtrCol, asrQtrWidth, asrQtrStyle.styler(), asrQtrBool, '1.25 ASR'), lineDrawing.new(asrHalfCol, asrHalfWidth, asrHalfStyle.styler(), asrHalfBool, '1.50 ASR'),
     lineDrawing.new(asrQtr3Col, asrQtr3Width, asrQtr3Style.styler(), asrQtr3Bool, '1.75 ASR'), lineDrawing.new(asrDubCol, asrDubWidth, asrDubStyle.styler(), asrDubBool, '2 ASR'), lineDrawing.new(qtrCol, qtrWidth, qtrStyle.styler(), qtrBool, '-.25 ASR'), 
     lineDrawing.new(halfCol, halfWidth, halfStyle.styler(), halfBool, '-.50 ASR'), lineDrawing.new(qtr3Col, qtr3Width, qtr3Style.styler(), qtr3Bool, '-.75 ASR'), lineDrawing.new(asrCol, asrWidth, asrStyle.styler(), asrBool, '-1 ASR'), 
     lineDrawing.new(asrQtrCol, asrQtrWidth, asrQtrStyle.styler(), asrQtrBool, '-1.25 ASR'), lineDrawing.new(asrHalfCol, asrHalfWidth, asrHalfStyle.styler(), asrHalfBool, '-1.50 ASR'), lineDrawing.new(asrQtr3Col, asrQtr3Width, asrQtr3Style.styler(), asrQtr3Bool, '-1.75 ASR'), 
     lineDrawing.new(asrDubCol, asrDubWidth, asrDubStyle.styler(), asrDubBool, '-2 ASR'))

//----------sessions----------//
session         = time(timeframe.period, sesh1time, tz)
startSession    = not na(session) and na(session[1])
endSession      = na(session) and not na(session[1])

split           = str.split(sesh1time,'-')
start           = split.get(0)
end             = split.get(1)

startHour       = str.tonumber(str.substring(start,0,2))
startMin        = str.tonumber(str.substring(start,2,4))
endHour         = str.tonumber(str.substring(end,0,2))
endMin          = str.tonumber(str.substring(end,2,4))

startTime       = timestamp(year, month, dayofweek, int(startHour), int(startMin))
int endTime     = na

if endHour < startHour
    endTime := timestamp(year, month, dayofweek+1, int(endHour), int(endMin))
else
    endTime := timestamp(year, month, dayofweek, int(endHour), int(endMin))

sessionLength = endTime - startTime

//----------variables----------//
var sessionRanges       = array.new<float>()
var lines               = array.new<line>()
var labels              = array.new<label>()
var float sessionHigh   = na
var float sessionLow    = na
var float sessionOpen   = na
var table asrLabel      = table.new(str.lower(yPos) + '_' + str.lower(xPos), 1, 1, tableBgColor, color.new(color.white,100), 1, color.new(color.white,100), 1)

//----------session data----------//
if startSession
    sessionHigh := high
    sessionLow  := low
    sessionOpen := open

if time == session
    sessionHigh := math.max(sessionHigh,high)
    sessionLow  := math.min(sessionLow,low)

if endSession
    sessionRanges.push(sessionHigh-sessionLow)
    if sessionRanges.size() > length
        sessionRanges.shift()

//----------conditions----------//
asr     = sessionRanges.size() > 0 ? sessionRanges.avg() : na
qtr     = asr / 4
half    = qtr * 2
qtr3    = qtr * 3

asrArr = array.from(sessionOpen + qtr, sessionOpen + half, sessionOpen + qtr3, sessionOpen + asr, sessionOpen + asr + qtr, 
     sessionOpen + asr + half, sessionOpen + asr + qtr3, sessionOpen + asr *2, sessionOpen - qtr, sessionOpen - half, 
     sessionOpen - qtr3, sessionOpen - asr, sessionOpen - asr - qtr, sessionOpen - asr - half, sessionOpen - asr - qtr3, sessionOpen - asr*2)

//----------lines-----------//
if startSession
    for l in lines
        l.delete()
    for l in labels
        l.delete()
    labels.clear()
    lines.clear()
    if showLines
        for [idx,lev] in asrArr
            setting = colArr.get(idx)
            if setting.showLev
                lines.push(line.new(time, lev, time + sessionLength, lev, xloc.bar_time, extend.none, setting.levCol, setting.levStyle, setting.levWidth))
                if showLabels
                    labels.push(label.new(labPos == 'Left' ? time : time + sessionLength, lev, setting.name + ' (' + str.tostring(lev,format.mintick) + ')', 
                         xloc.bar_time, color = color.new(color.white,100), style = labPos == 'Left' ? label.style_label_right : label.style_label_left, textcolor = setting.levCol))

if barstate.islast
    asrLabel.cell(0,0, 'ASR: $' + str.tostring(asr,format.mintick), text_color = tableTextColor, text_size = tableTextSize.txtSize())